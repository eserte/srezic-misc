#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: xmlgrep,v 1.2 2007/07/08 18:07:24 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2006 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Getopt::Long;
use XML::LibXML;

my %opt;
GetOptions(\%opt, "as-xml!", "parse-html!")
    or die "usage!";

my $xpath = shift @ARGV;
my @files = @ARGV;

my $p = XML::LibXML->new;
$p->pedantic_parser(0);
$p->recover(1);

if (@files) {
    for my $file (@files) {
	open my $fh, $file
	    or warn "Can't open $file: $!", next;
	xmlgrep($fh);
    }
} else {
    xmlgrep(\*STDIN);
}

sub xmlgrep {
    my($fh) = @_;
    my $buf = do {
	local $/;
	<$fh>;
    };

    my $doc;
    if ($opt{"parse-html"}) {
	$doc = $p->parse_html_string($buf);
    } else {
	$doc = $p->parse_string($buf);
    }
    $doc or return;

    my $root = $doc->documentElement;
    for my $node ($root->findnodes($xpath)) {
	if ($opt{"as-xml"}) {
	    print $node->toString, "\n";
	} else {
	    print $node->to_literal, "\n";
	}
    }
}

__END__

#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: xmlgrep,v 1.10 2007/07/26 21:44:20 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2006,2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Getopt::Long;
use File::Basename qw(basename);

my %opt;
GetOptions(\%opt,
	   "as-xml|asxml!",
	   "as-html|ashtml!",
	   "parse-html|parsehtml!",
	  )
    or usage();

if (basename($0) =~ m{htmlgrep}) {
    $opt{'parse-html'} = 1;
}

my $xpath = shift @ARGV;
my @files = @ARGV;

my $p;
if ($opt{'parse-html'}) {
    require HTML::TreeBuilder::XPath;
    $p = HTML::TreeBuilder::XPath->new;
} else {
    require XML::LibXML;
    $p = XML::LibXML->new;
    $p->pedantic_parser(0);
    $p->recover(1);
}

if (@files) {
    for my $file (@files) {
	open my $fh, $file
	    or warn "Can't open $file: $!", next;
	xmlgrep($fh);
    }
} else {
    xmlgrep(\*STDIN);
}

sub xmlgrep {
    my($fh) = @_;
    my $buf = do {
	local $/;
	<$fh>;
    };

    my $root;
    if ($p->isa("HTML::TreeBuilder::XPath")) {
	$p->parse($buf);
	$root = $p;
    } else {
	my $doc = $p->parse_string($buf);
	$doc or return;
	$root = $doc->documentElement;
	$root->setNamespaceDeclURI(undef, undef);
    }

    # my @result = map { $_->textContent } $root->findnodes($path);
    for my $node ($root->findnodes($xpath)) {
	if ($opt{"as-xml"}) {
	    if ($opt{'parse-html'}) {
		die "Output of XML for parsed HTML is not implemented";
	    }
	    print $node->toString, "\n";
	} elsif ($opt{"as-html"}) {
	    if (!$opt{'parse-html'}) {
		die "Output of HTML for parsed XML is not implemented";
	    }
	    if ($node->can('getValue')) { # for HTML::TreeBuilder::XPath::Attribute
		print $node->getValue, "\n";
	    } else {
		print $node->as_HTML, "\n";
	    }
	} else {
	    if ($node->can("to_literal")) {
		print $node->to_literal, "\n";
	    } elsif ($node->can("getValue")) { # for HTML::TreeBuilder::XPath::Attribute
		print $node->getValue, "\n";
	    } else {
		print $node->findvalue('.'), "\n";
	    }
	}
    }
}

sub usage {
    die <<EOF;
usage: $0 [-as-xml] [-parse-html] xpath [file ...]
EOF
}

__END__

=head1 NAME

xmlgrep - grep for XML or HTML files using XPath patterns

=head1 SYNOPSIS

    xmlgrep [-as-xml] [-parse-html] xpath [file ...]
    htmlgrep xpath [file ...]

=head1 EXAMPLES

Get all <a> links:

    xmlgrep -parse-html '/html/body//a/@href' some.html

or

    htmlgrep '/html/body//a/@href' some.html

=head1 AUTHOR

Slaven Rezic

=cut

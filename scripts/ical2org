#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: ical2org,v 1.1 2009/02/07 22:51:17 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2009 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Data::ICal;
use POSIX qw(strftime);
use Time::Local qw(timelocal);

for my $file (@ARGV) {
    my $ical;
    for my $val10_flag (0, 1) {
	$ical = Data::ICal->new(filename => $file, vcal10 => $val10_flag);
	last if $ical;
    }
    if (!$ical) {
	warn "Cannot recognize file $file as ical file '" . $ical->error_message . "', skipping...\n";
	next;
    }
    for my $entry (@{ $ical->entries }) {
	next if $entry->ical_entry_type ne 'VEVENT';
	my $props = $entry->properties;
	# XXX decoded_value?
	my $begin   = icaldate_to_orgdate(eval { $props->{dtstart}->[0]->value });
	my $end     = icaldate_to_orgdate(eval { $props->{dtend}->[0]->value });
	my $id      = icaluid_to_orgid(eval { $props->{uid}->[0]->value });
	my $summary = eval { $props->{summary}->[0]->value } || "no summary";
	print "** " . $summary . ($begin ? " $begin" : "") . ($end ? "--$end" : "") . "\n";
	if (defined $id) {
	    print "   :PROPERTIES:\n";
	    print "   :ID: $id\n";      
	    print "   :END:\n";
	}
    }
}

sub icaldate_to_orgdate {
    my $dt = shift;
    return if !defined $dt;
    if (my($Y,$M,$D,$h,$m,$s) = $dt =~ m{^(\d{4})(\d{2})(\d{2})T(\d{2})(\d{2})(\d{2})$}) {
	my $epoch = timelocal($s,$m,$h,$D,$M-1,$Y);
	strftime "<%Y-%m-%d %a %H:%M>", localtime $epoch;
    } else {
	warn "Cannot parse date '$dt'!";
	undef;
    }
}

sub icaluid_to_orgid {
    my $uid = shift;
    return if !defined $uid;
    $uid =~ s{^\s+TS-}{}; # XXX why?
    $uid;
}

__END__

=head1 NAME

ical2org - format ical entries into org-mode-compatible entries

=head1 SYNOPSIS

    ical2org ical.vcs
    # and then paste the output into an org-mode emacs buffer

=head1 PREREQUISITES

Data::ICal

=head1 AUTHOR

Slaven Rezic

=cut

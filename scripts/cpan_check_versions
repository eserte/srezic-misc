#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: cpan_check_versions,v 1.1 2007/09/24 22:34:48 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;

use Cwd qw(cwd);
use ExtUtils::Manifest qw(maniread);
use ExtUtils::MM_Unix;
use File::Compare qw(compare);

my $olddistdir = shift || die "old distribution directory?";
my $distdir = shift || cwd;
my $manifest_file = "$distdir/MANIFEST";
my $manifest = maniread($manifest_file);
for my $file (keys %$manifest) {
    if ($file =~ m{\.pm}) {
	my $new_file = $distdir."/".$file;
	my $old_file = $olddistdir."/".$file;
	if (!-e $old_file) {
	    warn "NOTE: $file is new...\n";
	    next;
	}
	if (compare($old_file,$new_file) != 0) {
	    my $old_version = MM->parse_version($old_file);
	    my $new_version = MM->parse_version($new_file);
	    if ($old_version eq $new_version) {
		warn "ERROR: $file differ, but version is still the same: $new_version\n";
	    }
	}
    }
}

__END__

#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: multiperltest,v 1.1 2006/03/30 06:41:25 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2002,2004 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use File::Basename;
use Cwd;
use Data::Dumper;
use Getopt::Long;

my $use_xnest;
my $xvfb_display = "localhost:121"; # force TCP connection to X11 because of stupid problems with my X11
my $need_x_server;

if (!GetOptions("xnest" => \$use_xnest,
		"display=s" => \$xvfb_display,
		"needx!" => \$need_x_server,
	       )) {
    die "usage";
}
(my $xvfb_display_nr = $xvfb_display) =~ s/^.*://;

$ENV{BATCH} = 1;

my @perl = (
	 {exec => 'perl5.8.8'},
	 {exec => 'perl5.8.7'},
	 {exec => 'perl5.8.6'},
	 {exec => 'perl5.8.3d'},
	 {exec => 'perl5.8.0'},
	 {exec => 'perl5.6.1'},
	 {exec => '/usr/bin/perl5.00503'},
	 {exec => 'perl5.9.0'},
	 {exec => 'perl5.00404'},
	 {exec => 'perl5.8.2d'},
	 {exec => 'perl5.8.1'},
# 	 {exec => '/usr/local/src/bleedperl/perl',
# 	  env => { PERL5LIB => '/usr/local/src/bleedperl/lib' },
# 	 }
	);

my $basedistdir = basename cwd;
if ($basedistdir =~ /^Tk-/) {
    $need_x_server = 1;
}

my $xvfb_pid;
if ($need_x_server) {
    if (open(PID, "/tmp/.X$xvfb_display_nr-lock")) {
	chomp(my $pid = <PID>);
	close PID;
	$pid+=0;
	warn "Kill old server with pid $pid ...\n";
	kill KILL => $pid;
    }

    warn "Starting X11 server $xvfb_display ...\n";
    $xvfb_pid = fork;
    if ($xvfb_pid == 0) {
	if ($use_xnest) {
	    exec("Xnest", "-geometry", "640x400", ":$xvfb_display_nr");
	} else {
	    exec("Xvfb", "-fp", "/usr/X11R6/lib/X11/fonts/misc", "-ac", ":$xvfb_display_nr");
	}
	die $!;
    }

    $ENV{DISPLAY} = $xvfb_display;
}

foreach my $perl (@perl) {
    print STDERR "#" x 70, "\n";
    print STDERR "# " . $perl->{exec} . "...\n";
    $perl->{success} = 0;
    local %ENV = %ENV;
    if ($perl->{env}) {
	while(my($k,$v) = each %{ $perl->{env} }) {
	    $ENV{$k} = $v;
	}
    }
    system($perl->{exec}, "Makefile.PL");
    if ($? != 0) {
	$perl->{why} = "Makefile.PL failed";
	warn "$perl->{why}...\n";
    } else {
	system("make");
	if ($? != 0) {
	    $perl->{why} = "make failed";
	    warn "$perl->{why}...\n";
	} else {
	    system("make", "disttest");
	    if ($? != 0) {
		$perl->{why} = "make disttest failed";
		warn "$perl->{why}...\n";
	    } else {
		$perl->{success} = 1;
	    }
	}
	system("make","realclean");
    }
}

if (defined $xvfb_pid) {
    kill KILL => $xvfb_pid;
}


print STDERR Data::Dumper->new([\@perl],[])->Indent(1)->Useqq(1)->Dump;

__END__

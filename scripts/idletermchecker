#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: idletermchecker,v 1.29 2008/09/13 15:04:18 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use File::Spec;
use IO::Select;
use IO::Socket::UNIX;
use Getopt::Long;

my $lsof = is_in_path("lsof");
undef $lsof; # XXX unfortunately lsof may become wild and eat memory
             # and loop wildly on freebsd...

my @terminals;
my $default_timeout  = 30;
my $default_timeout2 = 60;
my $timeout  = $default_timeout;
my $timeout2 = $default_timeout2;
my $local_timeout;
my $q; # be quiet on stderr
my $do_tk;
my $geometry;
my $do_server;
my $server_filename = "$ENV{HOME}/.idletermchecker-server";
my $server_pid;
my $permanently_shown;
my $do_remove;
GetOptions("t|timeout=i" => \$timeout,
	   "t2|timeout2=i" => \$timeout2,
	   "q" => \$q,
	   "tk" => \$do_tk,
	   "geometry=s" => \$geometry,
	   "s|server" => \$do_server,
	   "remove" => \$do_remove,
	  )
    or die "usage!";
if ($timeout  != $default_timeout &&
    $timeout2 == $default_timeout2) {
    # keep delta:
    $timeout2 += ($timeout - $default_timeout);
}
if ($timeout2 <= $timeout) {
    # must be greater
    $timeout2 = $timeout + 1;
}

my $remove_term_tk;

if ($do_remove && !$do_server) {
    die "The -remove option only works together with the -server option";
}

if ($do_server) {
    my $h = IO::Socket::UNIX->new(Type => SOCK_STREAM,
				  Peer => $server_filename,
				 );
    if (!$h) {
	if ($do_remove) {
	    die "-remove specified, but no running server found";
	} else {
	    warn "Can't connect to $server_filename, create a new server...\n";
	}
    } else {
	warn "Using existing server...\n";
	for my $term (@ARGV) {
	    if ($do_remove) {
		$h->print("remove $term\n");
	    } else {
		$h->print("add $term\n");
	    }
	}
	$h->close;
	exit 0;
    }
}
if ($do_tk) {
    check_tk(@ARGV);
} elsif (@ARGV) {
    @terminals = map { abbrev_to_dev($_) } @ARGV;
    check_terminals(\@terminals);
} else {
    check_stdin();
}

sub check_terminals {
    my($terminals_ref) = @_;
    my @terminals = @$terminals_ref;
    while() {
	my @timeouted;
	my $use_timeout = $local_timeout || $timeout;
	for my $terminal (@terminals) {
	    my($mtime) = (stat($terminal))[9];
	    if (!defined $mtime) {
		die "Cannot get mtime for <$terminal>";
	    }
	    if (time-$mtime >= $use_timeout) {
		push @timeouted, $terminal;
	    }
	}
	if (@timeouted) {
	    my $message = "No output anymore on terminal(s) <@timeouted> for $use_timeout seconds!";
	    my $do_exit = exit_dialog($message);
	    if ($do_exit) {
		my %timeouted = map {($_,1)} @timeouted;
		my @new_terminals;
		for (@terminals) {
		    push @new_terminals, $_ if !$timeouted{$_};
		}
		if (!@new_terminals) {
		    exit 0;
		} else {
		    @terminals = @new_terminals;
		}
	    }
	}
	sleep 1;
    }
}

sub check_stdin {
    my $s = IO::Select->new;
    $s->add(\*STDIN);

    while () {
	while () {
	    my @ready = $s->can_read($timeout);
	    if (!@ready) {
		last;
	    }
	    while () {
		my $buf;
		last if sysread(STDIN, $buf, 256, 0) < 256;
	    }
	}
	my $message = "No output anymore on terminal for $timeout seconds!";
	my $do_exit = exit_dialog($message);
	exit 0 if $do_exit;
    }
}

sub exit_dialog {
    my($message) = @_;
    undef $local_timeout;
    if (!$q) {
	print STDERR "$message\n";
    }

    if (is_in_path("alarm")) {
	system("alarm", "-nicedown");
    }
    if (is_in_path("tkmessage")) {
	system("tkmessage", "-bg", "red", "-fg", "white", "-buttons", "Continue:100,Abort:101,Later 1min:102,Later 5min:103,Later 30min:104", $message);
	my $st = $?/256;
	if ($st >= 102 && $st <= 104) {
	    $local_timeout = {102 => 1*60,
			      103 => 5*60,
			      104 =>30*60,
			     }->{$st};
	    return 0;
	} elsif ($st != 100) {
	    return 1;
	} else {
	    return 0;
	}
    } else {
	print STDERR "$message. Abort? (y/N) ";
	chomp(my $yn = <STDIN>);
	if ($yn =~ m{^y}) {
	    return 1;
	} else {
	    return 0;
	}
    }
}

sub abbrev_to_dev {
    my $abbrev = shift;
    if (File::Spec->file_name_is_absolute($abbrev)) {
	$abbrev;
    } elsif ($^O eq 'linux') {
	"/dev/pts/$abbrev";
    } else {
	"/dev/tty$abbrev";
    }
}

sub term_glob {
    if ($^O eq 'linux') {
	"/dev/pts/*";
    } else {
	"/dev/tty*";
    }
}

sub check_tk {
    my(@terminals) = @_;
    die "No terminals given..." if !@terminals;
    @terminals = map { abbrev_to_dev($_) } @ARGV;

    require Tk;
    my $mw = MainWindow->new;
    $mw->geometry($geometry) if $geometry;
    $mw->iconify unless $permanently_shown;

    $Tk::platform = $Tk::platform; # peacify -w
    if ($Tk::platform eq 'unix') {
	my($wrapper) = $mw->wrapper;
	# set sticky flag for gnome and fvwm2
	# XXX Hmmm, these are the old properties. What about new ones?
	eval q{
	    $mw->property('set','_WIN_STATE','CARDINAL',32,[1],$wrapper); # sticky
	    $mw->property('set','_WIN_LAYER','CARDINAL',32,[6],$wrapper); # ontop
	};
	warn $@ if $@;
    }

    my %l;
    my %f;
    my %age_l;

    my $remove_term = sub {
	my $term = shift;
	my $f = $f{$term}
	    or return;
	@terminals = grep { $_ ne $term } @terminals;
	if (!@terminals) {
	    $mw->after(1500, sub {
			   $mw->destroy;
			   exit 0;
		       });
	    $mw->messageBox(-message => "No more watchers, exiting...");
	} else {
	    $f->destroy;
	}
    };
    $remove_term_tk = $remove_term;

    my $remove_abbreved_term_sub = sub {
	my $new_term = shift;
	$new_term = abbrev_to_dev($new_term);
	$remove_term->($new_term);
    };

    my $add_term_sub = sub {
	my($term) = @_;
	my $f = $f{$term} = $mw->Frame->pack(-fill => 'x', -expand => 1);
	$l{$term} = $f->Label(-text => $term)->pack(-side => "left", -fill => 'x');
	$age_l{$term} = $f->Label(-width => 5, -anchor => "w")->pack(-side => "left", -fill => 'x');
	$f->Button(-text => 'x',
		   #-background => undef,
		   -foreground => 'red',
		   -padx => 0,
		   -pady => 0,
		   -relief => 'flat',
		   -bd => 0,
		   -command => sub {
## Don't ask anymore...
# 		       my $yesno = $f->messageBox(-type => "yesno",
# 						  -message => "Remove watcher <$term>?");
# 		       if ($yesno =~ m{^yes$}i) {
			   $remove_term->($term);
#		       }
		   })->pack(-side => "right");
    };
    for my $term (@terminals) {
	$add_term_sub->($term);
    }

    my $add_abbreved_term_sub = sub {
	my $new_term = shift;
	$new_term = abbrev_to_dev($new_term);
	if ($new_term && !grep { $new_term eq $_ } @terminals) {
	    push @terminals, $new_term;
	    $add_term_sub->($new_term);
	}
    };

    if ($do_server) {
	pipe(PARENT_RDR, CHILD_WTR);
	pipe(CHILD_RDR,  PARENT_WTR);
	CHILD_WTR->autoflush(1);
	PARENT_WTR->autoflush(1);

	my $pid = fork;
	if (!$pid) {		# child
	    # XXX with this the child process dumps core on exit (as of Tk 800.017)
	    #$SIG{INT} = sub { CORE::exit(0) };
	    die "Can't fork: $!" if !defined $pid;
	    close CHILD_RDR;
	    close CHILD_WTR;
	    my $socket_name = $server_filename;
	    unlink $socket_name;
	    my $h = IO::Socket::UNIX->new(
					  Type => SOCK_STREAM,
					  Local => $socket_name,
					  Listen => 1
					 );
	    die "No socket in $socket_name created" if !-S $socket_name;
	    chmod 0700 => $socket_name;
	    die "Couldn't create server socket" if !$h;

	    my $client;
	    while ($client = $h->accept()) {
		my($str) = scalar <$client>;
		print PARENT_WTR $str;
		close $client;
	    }
	    require POSIX;
	    POSIX::_exit(0);	# never reached
	} else {
	    close PARENT_RDR;
	    close PARENT_WTR;

	    $server_pid = $pid;

	    $mw->fileevent
		(\*CHILD_RDR, "readable",
		 sub {
		     if (!(kill 0 => $pid)) {
			 warn "Server isn't running (anymore)...";
			 # unfortunetaly, the documentation is not true,
			 # so we have to do this manually:
			 $mw->fileevent(\*CHILD_RDR, "readable", '');
			 return;
		     }
		     my($rin, $win, $ein) = ('','','');
		     vec($rin, fileno(CHILD_RDR),1) = 1;
		     $ein = $rin | $win;
		     my $nfound = select($rin, $win, $ein, 1);
		     if (!$nfound) {
			 warn "Timeout!\n";
			 return;
		     }

		     my $f = scalar <CHILD_RDR>;
		     my($action, @args) = split /\s+/, $f;
		     if ($action eq 'add') {
			 $add_abbreved_term_sub->($args[0]);
		     } elsif ($action eq 'remove') {
			 $remove_abbreved_term_sub->($args[0]);
		     } else {
			 warn "Unknown action $action @args";
		     }
		 });
	}
    }

    {
	my $new_term;
	my $control = MainWindow->new(-title => "Control idletermchecker");
	$control->Label(-text => "Add terminal:")->pack;
	my $e = $control->Entry(-textvariable => \$new_term)->pack;
	$e->focus;
	my $okb = $control->Button(-text => "OK",
				   -command => sub {
				       $add_abbreved_term_sub->($new_term);
				       $new_term = "";
				       $control->iconify;
				   },
				  )->pack;
	$e->bind("<Return>" => sub { $okb->invoke });
	$control->Checkbutton(-text => "Permanently shown",
			      -variable => \$permanently_shown,
			      -command => sub {
				  if ($permanently_shown) {
				      $mw->deiconify;
				      $mw->raise;
				  }
			      },
			     )->pack;
	$control->Button(-text => "Watch all terminals",
			 -command => sub {
			     my $term_glob = term_glob();
			     my %active_terminal = map {($_,1)} @terminals;
			     for my $term (glob($term_glob)) {
				 next if $active_terminal{$term};
				 next if !terminal_alive($term);
				 push @terminals, $term;
				 $add_term_sub->($term);
			     }
			 })->pack;
	$control->Button(-text => "Remove dead terminals",
			 -command => sub {
			     my @remove_terms;
			     for my $term (@terminals) {
				 if (!terminal_alive($term)) {
				     push @remove_terms, $term;
				 }
			     }
			     for my $term (@remove_terms) {
				 $remove_term->($term);
			     }
			 })->pack;

	$control->iconify;
    }

    while() {
	my @timeouted;
	my %age;
	my $use_timeout = $timeout;
	for my $terminal (@terminals) {
	    my($mtime) = (stat($terminal))[9];
	    if (!defined $mtime) {
		die "Cannot get mtime for <$terminal>";
	    }
	    my $age = time-$mtime;
	    if ($age >= $use_timeout) {
		push @timeouted, $terminal;
		$age{$terminal} = $age;
		$age_l{$terminal}->configure(-text => $age);
	    }
	}

	my %timeouted = map{($_,1)} @timeouted;
	my @non_timeouted = grep { !$timeouted{$_} } @terminals;
	for my $term (@non_timeouted) {
	    $_->configure(-background => 'green') for $l{$term}, $f{$term}, $age_l{$term};
	}
	if (@timeouted) {
	    if ($mw->state ne 'normal') {
		$mw->deiconify;
		$mw->raise;
	    }
	    for my $term (@timeouted) {
		my $bg;
		if ($age{$term} >= $timeout2) {
		    $bg = "#ff0000";
		} else {
		    $bg = sprintf "#%02x%02x00", 255*($age{$term}/$timeout2), (255-255*($age{$term}/$timeout2));
		}
		$_->configure(-background => $bg) for $l{$term}, $f{$term}, $age_l{$term};
	    }
	    my @remove_term;
	    for my $term (@timeouted) {
		if (-r $term && $lsof && $remove_term_tk) {
		    my $out = `$lsof -w $term`;
		    if (!$out) {
			warn "Terminal <$term> vanished";
			$remove_term_tk->($term);
		    }
		}
	    }
	} else {
	    $mw->iconify unless $permanently_shown;
	}
	$mw->update;
	sleep 1;
    }
}

sub terminal_alive {
    my $term = shift;
    my $res = `lsof $term 2>/dev/null | wc -l`;
    $res > 0;
}

sub server_cleanup {
    if (defined $server_pid) {
	# ein CTRL-C bekommt auch der Server-Prozeß ab, ansonsten
	# muß manuell abgeschossen werden
	# INT geht nicht, muss mindestens TERM sein
	kill 'TERM' => $server_pid;
    }
}

END { server_cleanup() }

# REPO BEGIN
# REPO NAME is_in_path /home/e/eserte/work/srezic-repository 
# REPO MD5 c9844dc5bfa1798245e913b3e53ba4e1

sub is_in_path {
    my($prog) = @_;
    return $prog if (file_name_is_absolute($prog) and -f $prog and -x $prog);
    require Config;
    %Config::Config = %Config::Config if 0; # cease -w
    my $sep = $Config::Config{'path_sep'} || ':';
    foreach (split(/$sep/o, $ENV{PATH})) {
	if ($^O eq 'MSWin32') {
	    # maybe use $ENV{PATHEXT} like maybe_command in ExtUtils/MM_Win32.pm?
	    return "$_\\$prog"
		if (-x "$_\\$prog.bat" ||
		    -x "$_\\$prog.com" ||
		    -x "$_\\$prog.exe" ||
		    -x "$_\\$prog.cmd");
	} else {
	    return "$_/$prog" if (-x "$_/$prog" && !-d "$_/$prog");
	}
    }
    undef;
}
# REPO END

# REPO BEGIN
# REPO NAME file_name_is_absolute /home/e/eserte/work/srezic-repository 
# REPO MD5 89d0fdf16d11771f0f6e82c7d0ebf3a8

BEGIN {
    if (eval { require File::Spec; defined &File::Spec::file_name_is_absolute }) {
	*file_name_is_absolute = \&File::Spec::file_name_is_absolute;
    } else {
	*file_name_is_absolute = sub {
	    my $file = shift;
	    my $r;
	    if ($^O eq 'MSWin32') {
		$r = ($file =~ m;^([a-z]:(/|\\)|\\\\|//);i);
	    } else {
		$r = ($file =~ m|^/|);
	    }
	    $r;
	};
    }
}
# REPO END

__END__

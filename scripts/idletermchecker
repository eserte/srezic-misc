#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: idletermchecker,v 1.10 2007/11/01 21:58:19 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

# Obsolete comments:
#   On FreeBSD:
#     kldload snp
#     sudo watch p2 | idlechecker.pl
#   On Linux:
#     Use snoop? http://sourceforge.net/projects/snoop/

use strict;
use File::Spec;
use IO::Select;
use Getopt::Long;

my @terminals;
my $timeout = 30; # mx.develooper.com is sloooow!
my $q; # be quiet on stderr
GetOptions("timeout=i" => \$timeout,
	   "q" => \$q,
	  )
    or die "usage!";

if (@ARGV) {
    @terminals = map { abbrev_to_dev($_) } @ARGV;
    check_terminals(\@terminals);
} else {
    check_stdin();
}

sub check_terminals {
    my($terminals_ref) = @_;
    my @terminals = @$terminals_ref;
    while() {
	my @timeouted;
	for my $terminal (@terminals) {
	    my($mtime) = (stat($terminal))[9];
	    if (!defined $mtime) {
		die "Cannot get mtime for <$terminal>";
	    }
	    if (time-$mtime >= $timeout) {
		push @timeouted, $terminal;
	    }
	}
	if (@timeouted) {
	    my $message = "No output anymore on terminal(s) <@timeouted> for $timeout seconds!";
	    my $do_exit = exit_dialog($message);
	    if ($do_exit) {
		my %timeouted = map {($_,1)} @timeouted;
		my @new_terminals;
		for (@terminals) {
		    push @new_terminals, $_ if !$timeouted{$_};
		}
		if (!@new_terminals) {
		    exit 0;
		} else {
		    @terminals = @new_terminals;
		}
	    }
	}
	sleep 1;
    }
}

sub check_stdin {
    my $s = IO::Select->new;
    $s->add(\*STDIN);

    while () {
	while () {
	    my @ready = $s->can_read($timeout);
	    if (!@ready) {
		last;
	    }
	    while () {
		my $buf;
		last if sysread(STDIN, $buf, 256, 0) < 256;
	    }
	}
	my $message = "No output anymore on terminal for $timeout seconds!";
	my $do_exit = exit_dialog($message);
	exit 0 if $do_exit;
    }
}

sub exit_dialog {
    my($message) = @_;
    if (!$q) {
	print STDERR "$message\n";
    }

    system("alarm", "-nicedown");
    system("tkmessage", "-bg", "red", "-fg", "white", "-buttons", "Continue:100,Abort:101", $message);
    my $st = $?/256;
    if ($st != 100) {
	return 1;
    } else {
	return 0;
    }
}

sub abbrev_to_dev {
    my $abbrev = shift;
    if (File::Spec->file_name_is_absolute($abbrev)) {
	$abbrev;
    } elsif ($^O eq 'linux') {
	"/dev/pts/$abbrev";
    } else {
	"/dev/tty$abbrev";
    }
}

__END__

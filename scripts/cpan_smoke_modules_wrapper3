#!/usr/bin/perl

use strict;
use FindBin;
use Getopt::Long;

sub run_smoker ($$$);
sub spec_usage (;$);

my $do_maintperl;
my $spec;
my $mirror_url = "http://cpan.cpantesters.org/";

GetOptions("maintperl" => \$do_maintperl,
	   "mirrorurl=s" => \$mirror_url,
	   "spec=s"    => \$spec,
	  )
    or die "usage: $0 [-mirrorurl ...] [-maintperl | -spec perlver,stableness,daterange]";

my $recent_url = $mirror_url;

if (!$do_maintperl && !$spec) {
    die "Please specify either -maintperl or -spec";
}

if ($spec) {
    my($perlver, $stableness, $range) = split /,/, $spec;
    spec_usage if !defined $range;
    spec_usage("perl version does not look like expected") if $perlver !~ m{^\d+\.\d+\.}; # does not look like a perl version (be not to strict here)
    spec_usage("stableness must be 'stable' or 'devel'") if $stableness !~ m{^(devel|stable)$};
    spec_usage("time range does not look like '-\${DAYS}d'") if $range !~ m{^-\d+.*};

    system('xterm-conf -f -title "INIT cpan_smoke_modules wrapper"');
    run_smoker $perlver, $stableness, $range;
    system('xterm-conf -f -title "FINISHED cpan_smoke_modules wrapper"');
} elsif ($do_maintperl) {
    # installs everything stable and tests also developer versions
    system('xterm-conf -f -title "INIT maintperl"');

    (getpwuid($<))[0] eq 'cpansand'
    	or die "Works only for cpansand!";

    run_smoker '5.18.1', 'devel', '-3d';
    run_smoker '5.18.1', 'stable', '-20d';

    run_smoker '5.19.7', 'devel', '-3d';
    run_smoker '5.19.7', 'stable', '-20d';

    run_smoker '5.8.9t', 'devel', '-3d';
    run_smoker '5.8.9t', 'stable', '-20d';

    run_smoker '5.16.3', 'devel', '-3d';
    run_smoker '5.16.3', 'stable', '-20d';

    run_smoker '5.10.1t', 'devel', '-3d';
    run_smoker '5.10.1t', 'stable', '-20d';

    system('xterm-conf -f -title "FINISHED maintperl"');
}

sub run_smoker ($$$) {
    my($perl_version, $stability, $interval) = @_;
    warn "*** $perl_version: Just test uploads last $interval, " . ($stability eq 'stable' ? 'only stable' : 'also devel') . " versions...\n";
    my $perl_exe = '/usr/perl' . $perl_version . '/bin/perl' . $perl_version;
    if (!-x $perl_exe) {
	$perl_exe = '/usr/local/bin/perl' . $perl_version;
	if (!-x $perl_exe) {
	    die "Cannot find a perl $perl_version, tried in /usr/perlXXX and $perl_exe";
	}
    }
    my $install_arg = ($stability eq 'stable' ? '-install' : '');
    #my $install_arg = ''; # never install, currently running as non-cpansand with sudo configured
    my $cmd = q{env PERL_BATCH=yes ~eserte/work/srezic-misc/scripts/cpan_smoke_modules -savereports -typescript -nosignalend } . $install_arg . q{ -perl } . $perl_exe . q{ -skiptested -r } . "-mirrorurl $mirror_url" . q{ -- `~eserte/work/srezic-misc/scripts/cpan_recent_uploads2 -onlydists -url } . $recent_url . q{ -from } . $interval . q{ -to -2m -v } . ($stability eq 'stable' ? '-stable' : '') . q{ | grep -v '\.pl$'`};
    warn "$cmd ...\n";
    system $cmd;
    if ($? == 2) {
	die "SIGINT";
    }
}

sub spec_usage (;$) {
    my $msg = shift;
    die "spec format is perlversion,stableness,daterange where
perlversion is something like '5.18.2'
stableness is either 'devel' or 'stable'
daterange is amount of days written as '-20d'
" . ($msg ? "$msg\n" : '');
}

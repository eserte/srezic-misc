#!/usr/bin/perl -w
# -*- perl -*-

#
# $Id: cpan_smoke_modules,v 1.11 2007/10/07 22:13:32 eserte Exp $
# Author: Slaven Rezic
#
# Copyright (C) 2007 Slaven Rezic. All rights reserved.
# This program is free software; you can redistribute it and/or
# modify it under the same terms as Perl itself.
#
# Mail: slaven@rezic.de
# WWW:  http://www.rezic.de/eserte/
#

use strict;
use Getopt::Long;
use CPAN::DistnameInfo;

my @perls;
my $do_action = "report";
my $warntimeout = 60;
my $n;
GetOptions("n" => \$n,
	   "install" => sub { $do_action = "install" },
	   "warntimeout=i" => \$warntimeout,
	   'perl=s@' => \@perls,
	  )
    or die "usage!";
my @dist_or_mods = @ARGV;
if (!@dist_or_mods) {
    die "dist or mod?";
}

if (!@perls) {
    @perls = qw(perl5.10.0 perl5.8.8 perl5.6.2 perl5.00505);
}

# no idletermchecker in no exec mode:
if ($n && $warntimeout) {
    $warntimeout = undef;
}

my $idletermchecker_pid;
if ($warntimeout) {
    if (is_in_path("idletermchecker")) {
	chomp(my $tty = `tty`);
	if ($tty) {
	    $idletermchecker_pid = fork;
	    if ($idletermchecker_pid == 0) {
		exec "idletermchecker", "-timeout", $warntimeout, "-q", $tty;
		die $!;
	    }
	} else {
	    print STDERR "NOTE: cannot get tty, idletermchecker will not run.\n";
	}
    } else {
	print STDERR "NOTE: idletermchecker is not available.\n";
    }
}

# XXX should get prefs dir from config and check for SlayMakefile
system("cd $ENV{HOME}/.cpan/prefs && slaymake dd-prefs");

{
    my @new_dist_or_mods;
    my %seen_dist;
    for my $dist_or_mod (@dist_or_mods) {
	my $d_info = CPAN::DistnameInfo->new($dist_or_mod);
	if (!$d_info->dist) { # it's probably a mod name
	    push @new_dist_or_mods, $dist_or_mod;
	} elsif ($seen_dist{$d_info->dist}) {
	    warn "Remove " . $d_info->distvname . " from list...\n";
	} else {
	    push @new_dist_or_mods, $dist_or_mod;
	    $seen_dist{$d_info->dist}++;
	}
    }
    @dist_or_mods = @new_dist_or_mods;
}

for my $perl (@perls) {
    next if !is_in_path($perl);
    print STDERR "#"x70,"\n";
    print STDERR "# $perl\n";
    my $dist_or_mods_line = join(", ", map { qq{"$_"} } @dist_or_mods);
    my @cmd = ($perl, "-MCPAN", "-e", $do_action.qq{($dist_or_mods_line)});
    if ($n) {
	unshift @cmd, "echo";
    }
    print STDERR "# @cmd\n";
    system @cmd;
    if ($? & 127) {
	die sprintf("Child died with signal %d, %s coredump\n",
		    ($? & 127),  ($? & 128) ? 'with' : 'without');
    }
}

END {
    #kill KILL => $idletermchecker_pid if defined $idletermchecker_pid;
}

# REPO BEGIN
# REPO NAME is_in_path /home/e/eserte/work/srezic-repository 
# REPO MD5 81c0124cc2f424c6acc9713c27b9a484

sub is_in_path {
    my($prog) = @_;
    return $prog if (file_name_is_absolute($prog) and -f $prog and -x $prog);
    require Config;
    %Config::Config = %Config::Config if 0; # cease -w
    my $sep = $Config::Config{'path_sep'} || ':';
    foreach (split(/$sep/o, $ENV{PATH})) {
	if ($^O eq 'MSWin32') {
	    # maybe use $ENV{PATHEXT} like maybe_command in ExtUtils/MM_Win32.pm?
	    return "$_\\$prog"
		if (-x "$_\\$prog.bat" ||
		    -x "$_\\$prog.com" ||
		    -x "$_\\$prog.exe" ||
		    -x "$_\\$prog.cmd");
	} else {
	    return "$_/$prog" if (-x "$_/$prog" && !-d "$_/$prog");
	}
    }
    undef;
}
# REPO END

# REPO BEGIN
# REPO NAME file_name_is_absolute /home/e/eserte/work/srezic-repository 
# REPO MD5 89d0fdf16d11771f0f6e82c7d0ebf3a8

BEGIN {
    if (eval { require File::Spec; defined &File::Spec::file_name_is_absolute }) {
	*file_name_is_absolute = \&File::Spec::file_name_is_absolute;
    } else {
	*file_name_is_absolute = sub {
	    my $file = shift;
	    my $r;
	    if ($^O eq 'MSWin32') {
		$r = ($file =~ m;^([a-z]:(/|\\)|\\\\|//);i);
	    } else {
		$r = ($file =~ m|^/|);
	    }
	    $r;
	};
    }
}
# REPO END

__END__

=head1 NAME

cpan_smoke_modules - test and report CPAN modules with more perls

=head1 SYNOPSIS

    cpan_smoke_modules module1 author/distribution.tar.gz ...

=head1 NOTES

Assumption is that distributions are sorted in reverse order, that is
newest first. Any later distribution version on the command line will
be removed from the list.

    
=head1 TODO

    * evtl. die recent-Liste http://search.cpan.org/recent automatisch
      holen und abarbeiten, dabei aber Duplikate (ältere Versionen)
      ignorieren (-> CPAN::DistInfo?)

    * evtl. auch das zuletzt getestete Modul merken und/oder
      ~/.cpanreporter/reports-sent.db verwenden, um zu sehen, welche
      Dists getestet werden müssen (leider sind dort die disabled=1
      markierten Dists nicht enthalten)

=head1 AUTHOR

Slaven ReziE<0x0107>

=cut
